{$CLEO .cs}
{$USE debug}
{$USE memory}
{$USE file}

script_name 'CleoTest'
debug_on

print_big_formatted "CLEO TESTING" {time} 5000 {style} TextStyle.MiddleSmaller

wait 5000 // wait for game to fade in
clear_prints

cleo_call @RUN_TESTS_DIR {argCount} 2 {args} "cleo:" "cleo_tests"

print_big_formatted "DONE" {time} 5000 {style} TextStyle.MiddleSmaller

terminate_this_custom_script



// arg 0 - base directory path
// arg 1 - directory name
:RUN_TESTS_DIR
    trace "~w~Testing module '%s'" 1@

    // process all test files
    5@ = allocate_memory 260
    string_format {buffer} 5@ {format} "%s\\%s\\*.s" 0@ 1@
    
    6@ = 0 // search handle
    7@ = allocate_memory 64

    if
        find_first_file 5@ {handle} 6@ {fileName} 7@
    then
        while true
            string_format {buffer} 5@ {format} "%s\\%s\\%s" 0@ 1@ 7@
            if
                does_file_exist 5@ // files only
            then
                stream_custom_script 5@
                get_last_created_custom_script 11@

                while is_script_running 11@
                    wait 0
                end
            end

            write_memory 7@ {size} 4 {value} 0 {vp} false
            if
                not find_next_file 6@ {fileName} 7@
            then
                break
            end
        end

        find_close 6@
    else
        trace "~r~No tests found!"
    end
    
    // process all sub directories
    string_format {buffer} 5@ {format} "%s\\%s\\*" 0@ 1@

    6@ = 0 // search handle
    write_memory 7@ {size} 4 {value} 0 {vp} false
    if
        find_first_file 5@ {handle} 6@ {fileName} 7@
    then
        while true
            string_format {buffer} 5@ {format} "%s\\%s\\%s" 0@ 1@ 7@
            8@ = read_memory 7@ {size} 4 {vp} false
            if and
                8@ <> 0x2E // "."
                8@ <> 0x2E2E // ".."
                does_directory_exist 5@ // directories only
            then
                string_format {buffer} 5@ {format} "%s\\%s" 0@ 1@
                cleo_call @RUN_TESTS_DIR {argCount} 2 {args} 5@ 7@
            end

            write_memory 7@ {size} 4 {value} 0 {vp} false
            if
                not find_next_file 6@ {fileName} 7@
            then
                break
            end
        end

        find_close 6@
    end
    
    free_memory 5@
    free_memory 7@
cleo_return
